#Include "PROTHEUS.ch"
#Include "GPER130.CH"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GPER130  ³ Autor ³ R.H. - Mauro          ³ Data ³ 26.04.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Recibo de Ferias                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GPER130(void)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS  ³  Motivo da Alteracao                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Carlos E. O.³11/11/13³M12RH01³ Retirada da funcao AjustaSx1 para       ³±±
±±³            ³        ³196704 ³ inclusao do fonte na P12.               ³±±
±±³Sidney O.   ³27/08/14³TPZPWZ ³ Criada validacao para as datas do grupo ³±±
±±³            ³        ³       ³ de perguntas GPR130.                    ³±±
±±³Flavio Corr ³16/06/15³TSPUL3 ³ Correção busca de ferias na SRF para    ³±±
±±³            ³        ³       ³ aviso ferias calculadas                 ³±±
±±³Renan Borges³30/12/15³TUCPPB ³ Ajuste para imprimir recibo de abono de ³±±
±±³            ³        ³       ³ férias corretamente independentemente   ³±±
±±³            ³        ³       ³ do filtro utilizado nos parametros.     ³±±
±±³            ³        ³       ³ aviso ferias calculadas                 ³±±
±±³Gustavo M.  ³01/03/16³TUOZGY ³ Ajuste para posicionar corretamente ao  ³±±
±±³            ³        ³       ³ dar o loop da SRA.					  ³±±
±±³P. Pompeu   ³04/04/16³TUUDL2 ³ Correção Valid. Pergunte Data Final     ³±±
±±³Gabriel A.  ³21/09/16³TVYMFC ³ Ajuste para gerar as verbas de          ³±±
±±³            ³        ³       ³ periculosidade no recibo de abono.      ³±±
±±|Claudinei S.|28/04/17|MRH-482|Ajustada FImprAvi() para considerar corre|±±
±±|            |        |       |tamente as faltas dos funcionários com   |±±
±±|            |        |       |Regime de Tempo Parcial quando elas forem|±±
±±|            |        |       |inferiores a 8 faltas.                   |±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function GPER130(cAlias,nRecno,nOpc,nTpImpFer)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis Locais (Basicas)                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cString		:= "SRA"                // ALIAS DO ARQUIVO PRINCIPAL (BASE)
Local aOrd			:= {STR0001,STR0002,STR0010,STR0009} 	//" Matricula "###" C.Custo + Matric" ### "C.Custo + Nome" ### "Nome"
Local nTotregs,nMult,nPosAnt,nPosAtu,nPosCnt,cSav20,cSav7 // REGUA
Local cDesc1		:= STR0003	//"Aviso / Recibo de F‚rias "
Local cDesc2		:= STR0004	//"Ser  impresso de acordo com os parametros solicitados pelo"
Local cDesc3		:= STR0005	//"usu rio."
Local cSavAlias,nSavRec,nSavOrdem
Local lPnm070TamPE	:= ExistBlock( "PNM070TAM" )
Local aOfusca		:= If(FindFunction('ChkOfusca'), ChkOfusca(), { .T., .F., {"",""} }) //[1]Acesso; [2]Ofusca; [3]Mensagem
Local aFldRel		:= {"RA_NOME","RA_NUMCP","RA_SERCP","RA_ADMISSA"}
Local lBlqAcesso	:= aOfusca[2] .And. !Empty( FwProtectedDataUtil():UsrNoAccessFieldsInList(aFldRel) )
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis Private(Basicas)                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private aReturn		:= {STR0006, 1,STR0007, 1, 2, 1, "",1 }	// "Zebrado"###"Administra‡„o"
Private nomeprog	:="GPER130"
Private anLinha		:= { },nLastKey := 0
Private cPerg		:="GPR130"
Private aStruSRF	:= {}
Private lImp		:= .F.
Private nTpImp		:= 0
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis Private(Programa)                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private cPd13o		:= Space(3)
Private cPd13oMat	:= Space(3)	
Private aCodFol		:= {}     // Matriz com Codigo da folha

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis UtinLizadas na funcao IMPR                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private Titulo		:= STR0008		//"RECIBO E AVISO DE FERIAS"
Private AT_PRG		:= "GPER130"
Private wCabec0		:= 3
Private wCabec1		:= ""
Private wCabec2		:= ""
Private wCabec3		:= ""
Private CONTFL		:= 1
Private nLi			:= 0
Private nTamanho	:= "P"

DEFAULT nTpImpFer	:= 0
DEFAULT cAlias		:= "SRA"
DEFAULT nRecno		:= 0
DEFAULT nOpc		:= 3

If !lBlqAcesso

	SetMnemonicos(xFilial("RCA"),NIL,.T.,"P_REGPARCI")
	P_REGPARCI	:= If( Type("P_REGPARCI") == "U", .F. , P_REGPARCI)

	cSavAlias := Alias()
	nSavRec   := RecNo()
	nSavOrdem := IndexOrd()

	If lPnm070TamPE
		IF ( ValType( uRetBlock := ExecBlock("PNM070TAM",.F.,.F.))  == "C" )
			nTamanho := uRetBlock
		Endif
	EndIf

	Pergunte("GPR130",.F.)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia controle para a funcao SETPRINT                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	wnrel:="GPER130"            //Nome Default do relatorio em Disco
	lImp := If(nTpImpFer > 0, .T., .F.)
	If lImp
		nTpImp	:= nTpImpFer
		wnrel:= SetPrint(cString,wnrel,"",@Titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd,,nTamanho,,.F.)
	Else
		wnrel:=SetPrint(cString,wnrel,cPerg,@Titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd,,nTamanho)
	EndIf

	If nLastKey = 27
		Return
	Endif

 	/*
    Parâmetros do aReturn
    aReturn - Preenchido pelo SetPrint()
    aReturn[1] - Reservado para formulário
    aReturn[2] - Reservado para numero de vias
    aReturn[3] - Destinatário
    aReturn[4] - Formato 1=Paisagem 2=Retrato
    aReturn[5] - Mídia 1-Disco 2=Impressora
    aReturn[6] – Porta ou arquivo
    aReturn[7] - Expressão do filtro
    aReturn[8] - Ordem a ser selecionada
    aReturn[9] [10] [n] - Campos a processar se houver
    */
	If IsBlind() .Or. lImp
		aReturn[5] := 1
		aReturn[6] := "GPER130"
	EndIf

	SetDefault(aReturn,cString)

	//Se pressionar -ESC- encerra o programa
	If nLastKey = 27
		Return
	Endif

	RptStatus({|lEnd| GP130Imp(@lEnd,wnRel,cString)},Titulo)

	dbselectarea(cSavAlias)
	dbsetorder(nSavOrdem)
	dbgoto(nSavrec)
Else
	Help(" ",1,aOfusca[3][1],,aOfusca[3][2],1,0)
EndIf

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GP130imp ³ Autor ³ R.H. - Mauro          ³ Data ³ 26.04.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Recibo de Ferias                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GPER130(void)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function GP130IMP(lEnd,WnRel,cString)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis Locais (Programa)                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//Arrays
Local aPeriodos  := {}

//Logicas
Local lTemCpoProg
Local lUltSemana
Local lImpFer	 := ExistBlock( "IMPFER",.F.,.T. )//variavel p/ PE "IMPFER"

//Numericas
Local nImprVias
Local nCnt
Local nCntTrf
Local i
Local nPosSem
Local nPosTbFer		:= 0
Local nTempoParc	:= 0

//Strings
Local cRot 			:= ""
Local cTipoRot 		:= ""
Local cPeriodo		:= ""
Local cSemana       := ""
Local cAnoMes       := ""
Local cFiliAtual	:= cFilAnt
Local nImpRecibo	:= 2
Local nImpAviso		:= 2
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Variaveis de Acesso do Usuario                               ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Local cAcessaSRA	:= &( " { || " + ChkRH( "GPER130" , "SRA" , "2" ) + " } " )
Local aTransfFun	:= {}

Private nSol13,nSolAb,nRecib,nRecAb,nRec13,cFilDe,cFilAte
Private cMatDe,cMatAte,cCcDe,cCcAte,cNomDe,cNomAte,cDtSt13
Private nFaltas	:= Val_Salmin:=0
Private Salario	:= SalHora := SalDia := SalMes := nSalPg := 0.00
Private lAchou		:= .F.
Private lTransf		:= .F.
Private aInfo		:= {}
Private aTabFer		:= {}    			// Tabela para calculo dos dias de ferias
Private aTabFer2	:= {}				// Tabela para calculo dos dias de ferias para regime de tempo parcial
Private aCodBenef	:= {}
Private nAviso,lImpAv,dDtfDe,dDtfAte,nImprDem, nVias

Private DaAuxI		:= Ctod("//")
Private DaAuxF		:= Ctod("//")
Private cAboAnt		:= If(GetMv("MV_ABOPEC")=="S","1","2") //-- Abono antes ferias
Private cAboPec		:= ""
Private nDBanco
Private lSomLiR		:= .F.
Private aVerbsAbo	:= {}
Private aVerbs13Abo	:= {}
Private nNumAvPg	:= 1 // Quantidade de avisos que serão impressos por página
Private nQtdAvImp	:= 0 // Contador de Avisos já impressos na execução atual

// Variável que indica que foi somente solicitada impressão pela Visualização das Férias
If lImp
	Do Case
		Case ( nTpImp == 1 ) ; ( nImpRecibo	:= 1 )	//"Recibo de Férias"
		Case ( nTpImp == 2 ) ; ( nImpAviso	:= 1 )	//"Aviso de Férias"
	End Case

	// Alterar Pergunte
	MV_PAR01	:=  2                  //-- Sol. 1ªParc.13º Sal ?
	MV_PAR02	:=  2                  //-- Sol. Abono Pecun. ?
	MV_PAR03	:=  nImpAviso          //-- Aviso de Ferias ?
	MV_PAR04	:=  nImpRecibo            //-- Recibo de Ferias ?
	MV_PAR05	:=  2                  //-- Recibo de Abono ?
	MV_PAR06	:=  2                  //-- Recibo 1ª Parc. 13º ?
	MV_PAR07	:=  1                  //-- Imp. Dt. Rec.Ferias ?
	MV_PAR08	:=  M->RH_DATAINI      //-- Periodo Ferias De ?
	MV_PAR09	:=  M->RH_DATAFIM      //-- Periodo Ferias Ate ?
	MV_PAR10	:=  M->RH_FILIAL       //-- Filial De ?
	MV_PAR11	:=  M->RH_FILIAL       //-- Filial Ate ?
	MV_PAR12	:=  M->RH_MAT          //-- Matricula De ?
	MV_PAR13	:=  M->RH_MAT          //-- Matricula Ate ?
	MV_PAR14	:=  SRA->RA_CC           //-- Centro de Custo De ?
	MV_PAR15	:=  SRA->RA_CC   	       //-- Centro de Custo Ate ?
	MV_PAR16	:=  Replicate("A",30)  //-- Nome De ?
	MV_PAR17	:=  Replicate("Z",30)  //-- Nome Ate ?
	MV_PAR18	:=  CToD("//") 		   //-- Dt. Solicitacao 13º ?
	MV_PAR19	:=  1                  //-- Nº de Vias ?
	MV_PAR20	:=  M->RH_DTRECIB 	   //-- Data de Pagto De ?
	MV_PAR21	:=  M->RH_DTRECIB 	   //-- Data de Pagto Ate ?
	MV_PAR22		:=  2                  //-- Imprime Demitidos ?
	MV_PAR23	:=  15                 //-- Dias Previos P/ Ab. Pecun ?
	MV_PAR24	:=  2                  //-- Impr. dados bancários ?
	MV_PAR25	:=  2                  //-- Soma Lic. Rem ?
	MV_PAR26	:=  2                  //-- Imprime 2 avisos por página ?

EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis Utilizadas para Parametros                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nOrdem  := aReturn[8]
nSol13  := mv_par01     //  SoLic. 1o. Parc. 13o.
nSolAb  := mv_par02     //  SoLic. Abono Pecun.
nAviso  := mv_par03     //  Aviso de Ferias
nRecib  := mv_par04     //  Recibo de Ferias
nRecAb  := mv_par05     //  Recibo de Abono
nRec13  := mv_par06     //  Recibo 1¦ parc. 13o.
nDtRec  := mv_par07     //  Imprime Periodo de Ferias
dDtfDe  := mv_par08     //  Periodo de Ferias De
dDtfAte := mv_par09     //  Periodo de Ferias Ate
cFilDe  := mv_par10     //  FiLial De
cFilAte := mv_par11     //  FiLial Ate
cMatDe  := mv_par12     //  Matricula De
cMatAte := mv_par13     //  Matricula Ate
cCcDe   := mv_par14     //  Centro De Custo De
cCcAte  := mv_par15     //  Centro De Custo Ate
cNomDe  := mv_par16     //  Nome De
cNomAte := mv_par17     //  Nome Ate
dDtSt13 := mv_par18     //  Data SoLic. 13o.
nVias   := mv_par19     //  No. de Vias
dDtPgDe := mv_par20	    //  Data de Pagamento De
dDtPgAte:= mv_par21	    //  Data de Pagamento Ate
nNumAvPg:= IIf(cPaisLoc == "BRA" .And. !Empty(mv_par26) .And. mv_par26 == 1, 2, 1)

If(Empty(dDtSt13) .and. nSol13 == 1 .and. cPaisLoc $ "BRA")
	Aviso(STR0016, STR0014, { STR0015 } )
	MS_FLUSH()
	Return
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica a base instalada, se for Brasil utiliza o param,	³
//³ caso contrario, fixa o param como 2 (Nao Imprime Demitidos)	³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nImprDem:= Iif( cPaisLoc == "BRA", mv_par22, 2 )
nDAbnPec:= IiF (cPaisLoc == "BRA", mv_par23, 15)
nDBanco := mv_par24		//  Imprime dados bancários
If !Empty(MV_PAR25) // Informa se deve somar os dias de licença remunerada ao período de gozo das férias.
	If MV_PAR25 == 1
		lSomLiR := .T.
	Else
		lSomLiR := .F.
	Endif
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica a existencia dos campos de programacao ferias no SRF³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
lTemCpoProg := fTCpoProg()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Pocisiona No Primeiro Registro Selecionado                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SRA")

If nOrdem == 1
	dbSetOrder(1)
ElseIf nOrdem == 2
	dbSetOrder(2)
ElseIf nOrdem == 3
	dbSetOrder(8)
ElseIf nOrdem == 4
	dbSetOrder(3)
Endif

If nOrdem == 1
	dbSeek( cFilDe + cMatDe,.T. )
	cInicio  := "SRA->RA_FILIAL + SRA->RA_MAT"
	cFim     := cFilAte + cMatAte
ElseIf nOrdem == 2
	dbSeek( cFilDe + cCcDe + cMatDe,.T. )
	cInicio  := "SRA->RA_FILIAL + SRA->RA_CC + SRA->RA_MAT"
	cFim     := cFilAte + cCcAte + cMatAte
ElseIf nOrdem = 3
	dbSeek(cFilDe + cCcDe + cNomDe,.T.)
	cInicio  := "SRA->RA_FILIAL + SRA->RA_CC + SRA->RA_NOME"
	cFim     := cFilAte + cCcAte + cNomAte
ElseIf nOrdem = 4
	dbSeek(cFilDe + cNomDe + cMatDe,.T.)
	cInicio  := "SRA->RA_FILIAL + SRA->RA_NOME + SRA->RA_MAT"
	cFim     := cFilAte + cNomAte + cMatAte
Endif

//--Setar impressora
@ 0,0 psay Avalimp(080)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega Regua de Processamento                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SetRegua(RecCount())

While !Eof() .And. &cInicio <= cFim

    nLi:= 0

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Movimenta Regua de Processamento                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	IncRegua()

	If lEnd
		@Prow()+1,0 PSAY cCancel
		Exit
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Consiste Parametrizacao do Intervalo de Impressao            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If (SRA->RA_MAT < cMatDe) .Or. (SRA->RA_MAT > cMatAte) .Or. ;
		(SRA->RA_CC  < cCcDe ) .Or. (SRA->RA_CC  > cCcAte) .Or.;
		(SRA->RA_NOME < cNomDe) .Or. (SRA->RA_NOME > cNomAte)
		SRA->(dbSkip(1))
		Loop
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Consiste Situacao do Funcionario                             ³
	//³ Inclusao do tratamento para Imprime Demitidos S/N no Brasil. ³
	//³ Se nao for Brasil considera-se o param como 2 (Nao imprime)	 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If (SRA->RA_SITFOLH $ "D" .AND. nImprDem <> 1) .Or. ;// 1 - Imprime Demitido = Sim
		(cPaisLoc == "BRA" .And. SRA->RA_CATFUNC $ "A|P")
		SRA->(dbSkip(1))
		Loop
	Endif

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Consiste Filiais e Acessos                                             ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
    If !( SRA->RA_FILIAL $ fValidFil() ) .Or. !Eval( cAcessaSRA )
		dbSelectArea("SRA")
		dbSkip()
		Loop
	EndIF
	cFilAnt := SRA->RA_FILIAL
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//| Carrega tabela para apuracao dos dias de ferias - aTabFer    |
	//| 1-Meses Periodo    2-Nro Periodos   3-Dias do Mes    4-Fator |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	cProcesso 	:= SRA->RA_PROCES
	cTipoRot	:= "3"
	cRot 		:= fGetCalcRot(cTipoRot)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Carrega o periodo atual de calculo (aberto)                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	fGetLastPer( @cPeriodo,@cSemana , cProcesso, cRot , .T., .F., @cAnoMes )

	aPeriodo    := {}
	aVerbsAbo   := {}
	aVerbs13Abo := {}

	//Carrega todos os dados do periodo
	fCarPeriodo( cPeriodo , cRot , @aPeriodo, @lUltSemana, @nPosSem)

	If Len(aPeriodo) > 0
		dDataDe := aPeriodo[nPosSem,3]
		dDataAte := aPeriodo[nPosSem,4]
	EndIf

	fTab_Fer(@aTabFer,,@aTabFer2)

	//Se as horas semanais forem inferiores a 26, e o Mnemonico P_REGPARCI estiver ativo,
	//utiliza os dias de férias da tabela S065 - Tabela de férias tempo parcial (Artigo 130A da CLT)
	If cPaisLoc == "BRA"
		nTempoParc := SRA->RA_HRSEMAN
		If SRA->RA_HOPARC == "1" .And. nTempoParc <= 25 .And. nTempoParc  > 0 .And. Len(aTabFer2) > 0	.And. P_REGPARCI
			nPosTbFer := Ascan(aTabFer2, { |X|  nTempoParc <= X[6] .And. nTempoParc > X[5] })
			If nPosTbFer > 0
				aTabFer := aClone(aTabFer2[nPosTbFer])
			Endif
		Endif
	EndIf

	lAchou := .F.
	lImpAv := If(nAviso==1 .or. nSolAb==1 .or. nSol13==1,.T.,.F.)   // Imprime Aviso e/ou So.Abono e/ou Sol.1.Parc.13. s/Calcular

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Procura No Arquivo de Ferias o Periodo a Ser Listado         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SRH" )
	If dbSeek( SRA->RA_FILIAL + SRA->RA_MAT )
		aPeriodos := {}
		While !Eof() .And. SRA->RA_FILIAL + SRA->RA_MAT == SRH->RH_FILIAL + SRH->RH_MAT
			If ( !(cPaisLoc $ "ANG") .And. (SRH->RH_DATAINI >= dDtfDe .And. SRH->RH_DATAINI <= dDtfAte) .And.;
			(SRH->RH_DTRECIB >= dDtPgDe .And. SRH->RH_DTRECIB <= dDtPgAte) ) .OR. ;
			( (cPaisLoc $ "ANG") .And. (SRH->RH_DTRECIB >= dDtPgDe .And. SRH->RH_DTRECIB <= dDtPgAte) )
				AAdd(aPeriodos, Recno() )
			EndIf
			dbSkip()
		Enddo

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Imprime Aviso de Ferias Caso nao tenha calculado             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If Len(aPeriodos) == 0
			dbSelectArea( "SRA" )
			If lImpAv
				FImprAvi(lTemCpoProg)
			Endif
			dbSelectArea( "SRA" )
			dbSkip()
			Loop
		Endif


        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
        //³ Carrega Informações das Transferências                       ³
        //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
        aTransfFun 	:= {}
        fTransfAll( @aTransfFun,,,.T.)

		For nCnt := 1 To Len(aPeriodos)
			dbSelectArea( "SRH" )
			dbGoTo(aPeriodos[nCnt])


            //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
            //³ Carrega Matriz Com Dados da Empresa                          ³
            //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

            For nCntTrf := 1 To Len(aTransfFun)
                lTranf := .F.
                If  aTransfFun[nCntTrf][7]>SRH->RH_DATAFIM
                    fInfo(@aInfo, aTransfFun[nCntTrf][8])
                    lTransf := .T.
                    Exit
                Endif
            Next cCntTrf

            If !lTransf
                fInfo(@aInfo,SRA->RA_FILIAL)
            Endif



			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Carrega Variaveis Codigos da Folha                           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !FP_CODFOL(@aCodFol,SRA->RA_FILIAL)
				Return
			Endif

			DaAuxI := SRH->RH_DATAINI
			DaAuxF := SRH->RH_DATAFIM

			If nRec13 == 1
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Monta a Variavel na Lista Para Nao Aparecer Recibo de Ferias ³
				//³ e Sim No Recibo De Abono e 13o.                              ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				cPd13o		:= aCodFol[22,1]
				cPd13oMat	:= aCodFol[1434,1]
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Busca os codigos de pensao definidos no cadastro beneficiario³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				fBusCadBenef(@aCodBenef, "131", {aCodfol[172,1]})
			Endif

			If nRecAb == 1 .AND. Empty(aVerbsAbo)

				//Verbas encontradas no GPEXIDC.PRX com 'abono' na descricao
				//
				aAdd(aVerbsAbo, aCodFol[74,1])
				aAdd(aVerbsAbo, aCodFol[205,1])
				aAdd(aVerbsAbo, aCodFol[617,1])
				aAdd(aVerbsAbo, aCodFol[622,1])
				aAdd(aVerbsAbo, aCodFol[623,1])

				For i := 632 To 635
					aAdd(aVerbsAbo, aCodFol[i,1])
				Next

				//Verbas encontradas no GPEXIDC1.PRX com 'abono' na descricao
				//
				For i := 1312 To 1327
					aAdd(aVerbsAbo, aCodFol[i,1])
				Next

				aAdd(aVerbsAbo, aCodFol[1330,1])
				aAdd(aVerbsAbo, aCodFol[1331,1])

				aAdd(aVerbsAbo, aCodFol[1407,1])
				aAdd(aVerbsAbo, aCodFol[1408,1])
				aAdd(aVerbsAbo, aCodFol[1409,1])
				aAdd(aVerbsAbo, aCodFol[1410,1])

				aAdd(aVerbs13Abo, aCodFol[79,1])
				aAdd(aVerbs13Abo, aCodFol[206,1])

			Endif

			lAchou := .T.
			nQtdAvImp := 0
			For nImprVias := 1 to nVias
				if lImpFer
					ExecBlock("IMPFER",.F.,.F.)
				else
					GPER990()
				endif
			Next nImprVias
			lImpAv := .F.
	    Next nCnt
    EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Imprime Aviso de Ferias Caso nao tenha calculado             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lImpAv
		FImprAvi(lTemCpoProg)
	Endif

	dbSelectArea("SRA")
	dbSkip()
Enddo
cFilAnt := cFiliAtual
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Termino do relatorio                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SRA")
Set Filter to
dbsetorder(1)

Set Device To Screen
If aReturn[5] == 1
	Set Printer To
	Commit
	ourspool(wnrel)
Endif
MS_FLUSH()

*-----------------------------
Static Function FImprAvi(lTemCpoProg)
*-----------------------------
Local dDtIniProg,nDiasAbono,nDiasFePro,nDiasDedFer,nPerc13Prog
Local nImprVias
Local cQry		:= ""
Local cData		:= dtos(dDtfDe)
Local cData1	:= dTos(dDtfAte)
Local nX		:= 1
Local nProg 	:= 0
Local lImpFer	 := ExistBlock( "IMPFER",.F.,.T. )//variavel p/ PE "IMPFER"
Local nDMes12  	:= 0
Local nDMes01 	:= 0
Local cDia2501  := SuperGetMv("MV_DIA2501", ,"N")
Local nDFerOrig	:= 0

Static lTemRF13

DEFAULT lTemRF13 := SRF->( ColumnPos( "RF_PER13S2" ) ) > 0

lMetadeFal := If( Type("lMetadeFal") == "U", .F. , lMetadeFal)
lTempoParc := If( Type("lTempoParc") == "U", .F. , lTempoParc)

If nAviso==1 .or. nSolAb==1 .or. nSol13==1 // Imprimi Aviso e/ou Sol.Abono e/ou Sol1.Parc.13. sem calcular

	aStruSRF  := If(Empty(aStruSRF),SRF->(dbStruct()),aStruSRF)

	cQry := GetNextAlias()
	BEGINSQL ALIAS cQry
			SELECT *
			FROM %table:SRF% SRF
			WHERE SRF.%notDel%
			AND RF_FILIAL= %exp:SRA->RA_FILIAL%
			AND RF_MAT=%exp:SRA->RA_MAT%
			AND RF_STATUS=%exp:'1'%
			AND ( (RF_DATAINI BETWEEN %exp:cData% AND %exp:cData1%) OR (RF_DATINI2 BETWEEN %exp:cData% AND %exp:cData1%)  OR (RF_DATINI3 BETWEEN %exp:cData% AND %exp:cData1%))
			ORDER BY RF_DATABAS
	ENDSQL
	For nX := 1 To Len(aStruSRF)
		If ( aStruSRF[nX][2] <> "C" )
			TcSetField(cQry,aStruSRF[nX][1],aStruSRF[nX][2],aStruSRF[nX][3],aStruSRF[nX][4])
		EndIf
	Next nX

	//-- Verifica se no Arquivo SRF Existe Periodo de Ferias
	If !(cQry)->(Eof())
		For nProg := 1 to 3
			dDtIniProg := CTOD("")
			nDiasFePro := 0
			nDiasAbono := 0
			nPerc13Prog:= 0

			If nProg == 1
				If (cQry)->RF_DATAINI >= dDtfDe .And. (cQry)->RF_DATAINI <= dDtfAte
					dDtIniProg := (cQry)->RF_DATAINI
					nDiasFePro := If(lTemCpoProg, (cQry)->RF_DFEPRO1, 0)
					nDiasAbono := If(lTemCpoProg, (cQry)->RF_DABPRO1, 0)
					nPerc13Prog:= (cQry)->RF_PERC13S
				EndIf
			EndIf
			If lTemCpoProg
				If nProg == 2
					If (cQry)->RF_DATINI2 >= dDtfDe .And. (cQry)->RF_DATINI2 <= dDtfAte
						dDtIniProg := (cQry)->RF_DATINI2
						nDiasFePro := (cQry)->RF_DFEPRO2
						nDiasAbono := (cQry)->RF_DABPRO2
						nPerc13Prog:= If(lTemRF13, (cQry)->RF_PER13S2,(cQry)->RF_PERC13S)
					EndIf
				EndIf
				If nProg == 3
					If (cQry)->RF_DATINI3 >= dDtfDe .And. (cQry)->RF_DATINI3 <= dDtfAte
						dDtIniProg := (cQry)->RF_DATINI3
						nDiasFePro := (cQry)->RF_DFEPRO3
						nDiasAbono := (cQry)->RF_DABPRO3
						nPerc13Prog:= If(lTemRF13, (cQry)->RF_PER13S3,(cQry)->RF_PERC13S)
					EndIf
				EndIf
			EndIf
			If !Empty(dDtIniProg)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Carrega Matriz Com Dados da Empresa                          ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				fInfo(@aInfo,SRA->RA_FILIAL)
				nDferven := nDferave := 0
				If (cQry)->RF_DVENPEN > 0 .And. !Empty((cQry)->RF_IVENPEN)
					M->RH_DATABAS := (cQry)->RF_IVENPEN
					M->RH_DBASEAT := (cQry)->RF_FVENPEN
					nDferven       := (cQry)->RF_DVENPEN
				Else
					M->RH_DATABAS := (cQry)->RF_DATABAS
					M->RH_DBASEAT := If( Empty((cQry)->RF_DATAFIM), fCalcFimAq((cQry)->RF_DATABAS), (cQry)->RF_DATAFIM )

					If nDiasFePro > 0
						nDferven := nDiasFePro
					Else
						//Calc_Fer(SRF->RF_DATABAS,dDatabase,@nDferven,@nDferave)
						nDferven := (cQry)->RF_DFERVAT
						nDferven := If (nDferVen <= 0,nDferave,nDferven)
					EndIf
				EndIf

				nDiasAviso 		:= GetNewPar("MV_AVISFER",aTabFer[3])  // Dias Aviso Ferias

				If !empty((cQry)->RF_ABOPEC)
					cAboPec := (cQry)->RF_ABOPEC
				Else
					cAboPec := cAboAnt		//-- cAboPec = 1 -> considera abono antes do periodo de gozo de ferias
				EndIf

				M->RH_DTAVISO  := fVerData(dDtIniProg - (If (nDiasAviso > 0, nDiasAviso,aTabFer[3])))
				M->RH_DFERIAS  := If( nDFerven > aTabFer[3] , aTabFer[3] , nDFerven )
				nDFerOrig	   := M->RH_DFERIAS
				M->RH_DTRECIB  := If(cAboPec=="1" .and. nDiasAbono > 0,DataValida(DataValida((dDtIniProg-nDiasAbono)-1,.F.)-1,.F.), DataValida(DataValida(dDtIniProg-1,.F.)-1,.F.))
				M->RF_TEMABPE  := (cQry)->RF_TEMABPE

				If (cQry)->RF_TEMABPE == "S" .And. !lTemCpoProg
					M->RH_DFERIAS -= If(nDiasAbono > 0, nDiasAbono, 10)
				Endif

				//--Abater dias de ferias Antecipadas
				If (cQry)->RF_DFERANT > 0
					M->RH_DFERIAS := Min(M->RH_DFERIAS, aTabFer[3]-(cQry)->RF_DFERANT)
				Endif

				// Abate Faltas  do cad. Provisoes
				If ( (cQry)->RF_DFALVAT + (cQry)->RF_DFALAAT ) > 5
					nDFaltaV := (cQry)->RF_DFALVAT + (cQry)->RF_DFALAAT
					TabFaltas(@nDFaltaV)

					If (nDFaltaV > 0 .and. nDiasAbono > 0 )

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³  Se tiver faltas e abono, calcular os dias de ferias\abono proporcional as faltas.|
					//³	 Exemplo: 20 dias ferias                                                          |
					//³	          10 dias de abono e                                                      |
					//³ 		  10 Faltas = deduzir 6 dias das ferias. 		 					      |
					//³           Regra do abono: 1/3 dos dias de ferias.                                 |
					//³			  Como funcionario teve 10 faltas, ele tem direito a apenas 24 dias de    |
					//³           ferias, e nao 30. Os dias de feria e abono devem ser proporcionais aos  |
					//³           dias de direito de ferias.                                              |
					//³           Dias de Direito = 24													  |
					//³           Dias de Abono   =  8 (24 / 3 = 1/3 dos dias de direito )                |
					//³           Dias de Ferias  = 16 (24 - 8 dias de abono) 							  |
					//³           Total de Ferias + Abono  = 24 Dias 									  |
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

						nDiasDedFer   := ( nDiasFePro - ( If(!lMetadeFal, If(!lTempoParc, nDFaltaV, 0), nDiasFePro / 2) - nDiasAbono ) )

						If nDiasDedFer > 0
							M->RH_DFERIAS := nDiasDedFer - NoRound( ( ( nDiasFePro + nDiasAbono ) - If(!lMetadeFal, If(!lTempoParc, nDFaltaV, 0), nDiasFePro / 2) ) / 3 )
						Else
							M->RH_DFERIAS -= (If(!lMetadeFal, If(!lTempoParc, nDFaltaV, 0), nDiasFePro / 2))
						EndIf

					Else
						M->RH_DFERIAS -= (If(!lMetadeFal, If(!lTempoParc, nDFaltaV, 0), nDiasFePro / 2))
					EndIf
				Endif

				M->RH_PERC13S := nPerc13Prog

				DaAuxI := dDtIniProg
				DaAuxF := dDtIniProg + M->RH_DFERIAS - 1

				If cDia2501 == 'S'
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Verifica se Deve Considerar dias 24/12, 25/12, 31/12 e 01/01 ³
					//| como licenca remunerada.                                     |
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					fChkLicRem( dDtIniProg, DaAuxF, @nDMes12, @nDMes01, cDia2501)
					If nDMes12 + nDMes01 > 0
						DaAuxF += nDMes12 + nDMes01
					EndIf
				Endif
				
				//Imprime relatório se dias calculados for POSITIVO ou em caso NEGATIVO, validar com os dias de direito
				If M->RH_DFERIAS > 0 .Or. (M->RH_DFERIAS <= 0 .And. aTabFer[3]+M->RH_DFERIAS > 0)
					If M->RH_DFERIAS <= 0
						M->RH_DFERIAS := Iif(nDFerOrig > aTabFer[3]+M->RH_DFERIAS, aTabFer[3]+M->RH_DFERIAS, nDFerOrig)
					EndIf
					nQtdAvImp := 0
					For nImprVias := 1 to nVias
						if lImpFer
							ExecBlock("IMPFER",.F.,.F.)
						else
							GPER990()
						endif
					Next
				Endif
			EndIf
		Next nProg
	Endif
	(cQry)->(dbCloseArea())
Endif

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³FAbPecun	ºAutor  ³Gustavo M.			 º Data ³  22/06/2012 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Ajuste de  perguntas.                                       º±±
±±º          ³                                                            º±±                                                k
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPER130                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±± /*/

Function FAbPecun()

IF MV_PAR23 < 15
	MsgInfo( OemToAnsi(STR0012) )
	MV_PAR23 := 15
Endif

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³G130ValDataºAutor  ³Equipe RH           º Data ³  25/07/2014 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Compara datas                                                º±±
±±º          ³                                                             º±±                                                k
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPER130                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±± /*/

Function G130ValD(dDataIni, dDataFim)
	Local lRet

	If !Empty(dDataFim)
		lRet := dDataFim >= dDataIni

		If !lRet
			MsgInfo( STR0013 )
		Endif
	EndIf

Return lRet
